plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group 'net.thedudemc'
archivesBaseName = 'DudeConfig'
version '0.5.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.jetbrains:annotations:20.1.0'
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'DudeConfig'
                artifactId = 'dudeconfig'
                packaging = 'jar'
                description = 'General configuration library.'
                url = 'https://github.com/jmilthedude/DudeConfig'

                scm {
                    connection = 'scm:git:git://github.com/jmilthedude/DudeConfig.git'
                    developerConnection = 'scm:git:ssh://github.com/jmilthedude/DudeConfig.git'
                    url = 'https://github.com/jmilthedude/DudeConfig'
                }

                licenses {
                    license {
                        name = 'GPL-3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'jmilthedude'
                        name = 'Justin Miller'
                        email = 'jmilthedude@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
//        maven {
//            name = "ossrh"
//            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
//            credentials {
//                username = findProperty('ossrhUsername')
//                password = findProperty('ossrhPassword')
//            }
//        }
        // Uncomment this only if your version ends with -SNAPSHOT, you'll get a 400 otherwhise.

        maven {
            name = "ossrhSnapshot"
            url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }

}

signing {
    sign publishing.publications.mavenJava
}
